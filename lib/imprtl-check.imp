
    ! Special hidden routines inserted by the pass2 of the compiler
    ! They act as pseudo perm routines, but are hidden as an %integer %function
    ! They all return a value of the specified parameter in the %EAX register

    ! They can be optionally switched on by %control or %diagnose
    ! These routines can trigger an %event X,Y,Z as a side-effect
    ! should the embedded test fail.

    ! "impcheck1" checks if x in range -32768..32767
    !   - failure returns %EAX = 0 and triggers %event 1,6,x
    !   - success returns %EAX = x
    %external %routine imp check 1( %integer x )
        %if (-32768 <= x <= 32767) %start
            *mov _ %eax,x
        %finish %else %start
            %if (x < 0) %start
                *mov _ %eax,#-32768
            %finish %else %start
                *mov _ %eax,#32767
            %finish
            %signal 1,6,x
        %finish
    %end

    ! "impcheck2" checks if x in range -32768..32767 AND (x # 0)
    !   - failure returns %EAX = 0 and triggers %event 1,6,x
    !   - success returns %EAX = x
    %external %routine imp check 2( %integer x)
        %if (-32768 <= x <= 32767) %and (x # 0) %start
            *mov _ %eax,x
        %finish %else %start
            %if (x < 0) %start
                *mov _ %eax,#-32768
            %finish %else %start
                *mov _ %eax,#32767
            %finish
            %signal 1,6,x
        %finish
    %end

    ! "impcheck3" checks if (x # 0)
    !   - failure returns %EAX = 1 and triggers %event 1,4
    !   - success returns %EAX = x
    %external %routine imp check 3( %integer x)
        %if (x # 0) %start
            *mov _ %eax,x
        %finish %else %start
            *mov _ %eax,#0
            %signal 1,4
        %finish
    %end

    { Sign extend a %short into an %integer }
    %external %routine s2i( %short s )
        *movsx_ %EAX,s
    %end

    { Zero extend a %byte into an %integer }
    %external %routine b2i( %byte b )
        *mov_ %EAX,b
        *and_ %eax,#255
    %end

%endoffile
