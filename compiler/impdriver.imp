
%externalroutinespec PASS1(%integername No stats, No Faults, No Warnings, %integer Options)
%externalroutinespec PASS2(%integername No stats, No Faults, %integer Options)

%external %routine impdriver %alias "__impmain"

    %integer No stats, No Faults, No Warnings
    %integer Options
    %string(255) imp param, imp defs, imp mode
    %string(255) imp source, imp prefix, imp extension
    %string(255) def file, imp file, icd file, ibj file, list file, code file

    %include "Stream3L.inc"
    %include "Option3L.inc"

    %predicate run pass( %string(255) mode, pass )
        %string(255) I,J

        %true %if mode = ""

        to upper( pass )
        to upper( mode )
        %true %if mode -> I.(pass).J
        %false
    %end

!    printstring("ARG#=");write(getargcount,0);newline
!    printstring("PROG=".getarg(0));newline

    imp defs   = ""
    imp source = ""
    imp mode   = ""
    imp defs   = getarg(1) %if (getargcount > 1)
    imp source = getarg(2) %if (getargcount > 2)
    imp mode   = getarg(3) %if (getargcount > 3)

!printstring("DEFS=".imp defs);newline
!printstring(" SRC=".imp source);newline
!printstring("MODE=".imp mode);newline

    %if (imp defs # "") %and (imp source # "") %start

        def file  = imp defs
        %if imp source -> imp prefix.(".imp") %start
            imp extension = ".imp"
        %finish %else %if imp source -> imp prefix.(".i") %start
            imp extension = ".i"
        %finish %else %start
            printstring("Illegal file extension. Legal values are (.i .imp)")
            newline
            imp prefix = ""
            imp extension = ""
            exit(1)
        %finish

        def file  = imp defs
        imp file  = imp prefix.imp extension
        icd file  = imp prefix.".icd"
        ibj file  = imp prefix.".ibj"
        list file = imp prefix.".lst"
        code file = imp prefix.".cod"

!printstring("IMP FILE=".imp file); newline
!printstring("ICD FILE=".icd file); newline
!printstring("IBJ FILE=".ibj file); newline
!printstring("LiST FILE=".list file); newline
!printstring("CODE FILE=".code file); newline
  
        No Stats = 0
        No Warnings = 0
        No Faults = 0
        options = LL Predef!LL List!LL Report

        options = options!XX Show ICode %if (get env as integer( "IMP_DIAGNOSE" )&16 # 0)

        %if run pass( imp mode, "pass1" ) %start
            open input( source, imp file )
            open input( predef in, def file )
            open binary output( icode out, icd file )
            open output( listing, list file )
            select input( predef in )

            PASS1(No stats, No Faults, No Warnings, Options)

            select output(report)
            printstring("#Stats=");write(No Stats,0); newline
            printstring("#Warnings=");write(No Warnings,0); newline
            printstring("#Faults=");write(No Faults,0); newline

            select output ( icode out )
            close output
            select output( listing )
            close output
            select input( source )
            close input
        %finish

        %if (No Faults = 0) %and run pass( imp mode, "pass2" ) %start

            open binary input( icode in2, icdfile )
            open input( source, impfile )
            open output( object out, ibjfile )
            open output( listing, codefile )

            PASS2(No Stats, No Faults, Options )

            select output( object out )
            close output
            select output( listing )
            close output
            select input( source )
            close input
        %finish

    %finish %else %start
        select output(report)
        print string( "    impdriver has missing parameters" );          newline
                                                                         newline
        print string( "    Mandatory parameters" );                      newline
        print string( "    Arg(1): IMP core routine definitions file" ); newline
        print string( "    Arg(2): IMP source file" );                   newline
                                                                         newline
!        print string( "    Optional parameters" );                       newline
!        print string( "    Arg(3): If present => %short == 2 bytes" );   newline
!    
         newline
         exit(-1)
    %finish

%end { of "impdriver" }
%endoffile
